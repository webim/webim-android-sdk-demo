import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

repositories {
    maven { url 'https://jitpack.io' }
}

android {
    compileSdkVersion 31
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId project.property('demo_applicationId')
        minSdkVersion 21
        targetSdkVersion 31
        versionCode project.property('versionCode').toString().toInteger()
        versionName project.property('versionName')
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    boolean signRelease = !project.property('keystore.path').toString().isEmpty()

    if(signRelease)
        signingConfigs {
            release {
                storeFile file(project.property('keystore.path'))
                storePassword project.property('keystore.password')
                keyAlias project.property('keystore.key.release.alias')
                keyPassword project.property('keystore.key.release.password')
            }
        }

    buildTypes {
        release {
            if(signRelease)
                signingConfig signingConfigs.release
            ext.betaDistributionEmails = project.property('betaDistributionEmails')
            ext.betaDistributionReleaseNotes = project.property('betaDistributionReleaseNotes')
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${gitBranchName()} (${getCurrentDate()}).apk"
                }
            }
        }
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


static String gitBranchName() {
    String defaultName = "debug-build"
    ProcessBuilder builder = new ProcessBuilder()
        .directory(new File(System.getProperty("user.dir")))
        .command("git", "rev-parse", "--abbrev-ref", "HEAD")

    try {
        Process process = builder.start()
        BufferedReader bufferedReader = process.inputStream.newReader()
        return bufferedReader.readLine()
    } catch (Exception exception) {
        println(exception.printStackTrace())
        return defaultName
    }
}

static String getCurrentDate() {
    Date currentDate = new Date()
    return new SimpleDateFormat("dd.MM.yyyy").format(currentDate)
}

dependencies {
    implementation project(':webimclientsdkandroid')
    implementation 'com.astuetz:pagerslidingtabstrip:1.0.1'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.google.firebase:firebase-messaging:23.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.4'
    implementation 'com.google.firebase:firebase-analytics:20.0.0'
    implementation 'com.github.thijsk:TouchImageView:v1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
}
