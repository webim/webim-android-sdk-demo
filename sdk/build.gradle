apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

version = project.property('versionName')
group = project.property('sdk_group')

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    resourcePrefix 'webimsdk__'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }
    buildTypes {
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.2.0'
    api 'com.squareup.retrofit2:converter-gson:2.2.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.5.0'
    implementation 'net.zetetic:android-database-sqlcipher:4.4.3@aar'
    implementation "androidx.sqlite:sqlite:2.1.0"
    implementation 'androidx.security:security-crypto:1.1.0-alpha01'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-core:3.3.3'
}

def siteUrl = project.property('websiteUrl')     // Homepage URL of the library
def gitUrl = project.property('gitUrl')          // Git repository URL

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Webim Mobile SDK for Android'
                packaging 'jar'
                description 'Webim Mobile SDK enables Android developers to implement chats into their mobile apps for communications between users and agents.'
                url siteUrl

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mshengeliia'
                        name 'Maksim Shengeliia'
                        email 'mshengeliia@webim-team.ru'
                    }
                    developer {
                        id 'nkaberov'
                        name 'Nikita Kaberov'
                        email 'nkaberov@webim.ru'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

signing {
    sign configurations.archives
}

task findConventions {
    leftShift {
        println project.getConvention()
    }
}